buildscript {
	ext.versions = [
		java     : '1.8',
		kotlin   : '1.0.3',
		slf4j    : '1.7.21',
		log4j    : '2.6.2',
		spring   : [
			framework: '4.3.1.RELEASE',
			data     : [
				core: '1.12.2.RELEASE',
				jpa : '1.10.2.RELEASE'
			]
		],
		seis     : [
			macAppBundle: '2.1.6',
			launch4j    : '1.6.2'
		],
		h2       : '1.4.192',
		hikari   : '2.4.7',
		javax    : [
			jpa: '1.0.0.Final'
		],
		hibernate: [
			core: '5.1.0.Final'
		],
		junit    : '4.12'
	]

	repositories {
		jcenter()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}

	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin",
			"gradle.plugin.edu.sc.seis:macAppBundle:$versions.seis.macAppBundle",
			"gradle.plugin.edu.sc.seis.gradle:launch4j:$versions.seis.launch4j"
	}
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'edu.sc.seis.macAppBundle'
apply plugin: 'edu.sc.seis.launch4j'

sourceCompatibility = versions.java
targetCompatibility = versions.java

mainClassName = 'io.whatthedill.pcre.Main'
macAppBundle {
	mainClassName = project.mainClassName
	bundleJRE = true
	javaProperties.put("apple.laf.useScreenMenuBar", "true")
}

launch4j {
	mainClassName = project.mainClassName
}

idea {
	project {
		jdkName = versions.java
		languageLevel = versions.java
		vcs = 'Git'
	}

	module {
		excludeDirs += file('gradle')
	}
}

repositories {
	jcenter()
}

configurations {
	compile.transitive = false
}

dependencies {
	compile project(':spring-fx'),
		project(':telemetry'),
		"org.jetbrains.kotlin:kotlin-stdlib:$versions.kotlin",
		"org.slf4j:slf4j-api:$versions.slf4j",
		"org.slf4j:slf4j-ext:$versions.slf4j",
		"org.springframework:spring-core:$versions.spring.framework",
		"org.springframework:spring-context:$versions.spring.framework",
		"org.springframework:spring-beans:$versions.spring.framework",
		"org.springframework:spring-jdbc:$versions.spring.framework",
		"com.zaxxer:HikariCP:$versions.hikari",
		"org.springframework.data:spring-data-jpa:$versions.spring.data.jpa",
		"org.springframework.data:spring-data-commons:$versions.spring.data.core",
		"org.hibernate.javax.persistence:hibernate-jpa-2.1-api:$versions.javax.jpa"

	runtime "org.apache.logging.log4j:log4j-core:$versions.log4j",
		"org.apache.logging.log4j:log4j-slf4j-impl:$versions.log4j",
		"org.slf4j:jcl-over-slf4j:$versions.slf4j",
		"org.slf4j:log4j-over-slf4j:$versions.slf4j",
		"com.h2database:h2:$versions.h2",
		"org.hibernate:hibernate-core:$versions.hibernate.core",
		"org.hibernate:hibernate-entitymanager:$versions.hibernate.core"

	testCompile "org.jetbrains.kotlin:kotlin-test:$versions.kotlin",
		"org.jetbrains.kotlin:kotlin-test-junit:$versions.kotlin"
}

task wrapper(type: Wrapper) {
	gradleVersion = project.property('gradle.wrapper.version')
}

startScripts {
	defaultJvmOpts << "-Dapple.laf.useScreenMenuBar=true"
}
